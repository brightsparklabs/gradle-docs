/*
 * Maintained by brightSPARK Labs.
 * www.brightsparklabs.com
 *
 * Refer to LICENSE at repository root for license details.
 */

// -----------------------------------------------------------------------------
// PLUGINS
// -----------------------------------------------------------------------------

plugins {
    // Apply the Java Gradle plugin development plugin to add support for developing Gradle plugins.
    id "java-gradle-plugin"
    id "com.gradle.plugin-publish" version "1.3.0"
    id 'maven-publish'

    // Apply the Groovy plugin to add support for Groovy.
    id "groovy"

    // Apply the Java plugin to add support for Java.
    id "java"

    // Apply the brightSPARK Labs plugin for standardisation.
    id "com.brightsparklabs.gradle.baseline" version "4.8.0"
}

// -----------------------------------------------------------------------------
// REPOSITORIES/DEPENDENCIES
// -----------------------------------------------------------------------------

repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    mavenCentral()
}

dependencies {
    implementation(
            'com.hubspot.jinjava:jinjava:2.7.3',
            // JRuby (used by Asciidoctor) does not support snakeyaml version 2 yet.
            //   org.jruby.exceptions.LoadError: (LoadError) bad SnakeYAML version 2.2, required 1.21 or higher;
            'org.yaml:snakeyaml:1.33',
            // Add external Gradle plugins here so they can be added via our plugin
            // NOTE: Obtain plugin dependency strings from plugins.gradle.org page
            'org.asciidoctor:asciidoctor-gradle-jvm:4.0.4',
            'org.asciidoctor:asciidoctor-gradle-jvm-pdf:4.0.4',
            )
    // Use the awesome Spock testing and specification framework.
    testImplementation "org.spockframework:spock-core:2.3-groovy-4.0"
}

// -----------------------------------------------------------------------------
// PROJECT IDENTIFICATION
// -----------------------------------------------------------------------------

project.description = "Applies brightSPARK Labs standardisation to project documentation"
project.ext.scm     = "https://github.com/brightsparklabs/gradle-docs.git"
project.ext.url     = "https://github.com/brightsparklabs/gradle-docs"

// -----------------------------------------------------------------------------
// java-gradle-plugin
// -----------------------------------------------------------------------------

gradlePlugin {
    website = project.url
    vcsUrl = project.scm
    // Define the plugin.
    plugins {
        docs {
            id = "com.brightsparklabs.gradle.docs"
            displayName = "Gradle Docs at brightSPARK Labs"
            description = project.description
            implementationClass = "com.brightsparklabs.gradle.docs.DocsPlugin"
            tags = [
                "brightsparklabs",
                "docs",
                "asciidoc",
                "asciidoctor",
            ]
        }
    }
}

// Add a source set for the functional test suite
sourceSets {
    functionalTest {
    }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)
configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

// Add a task to run the functional tests
task functionalTest(type: Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

check {
    // Run the functional tests as part of `check`
    dependsOn(tasks.functionalTest)
}

// Should not be needed with "com.gradle.plugin-publish:1.0.0"
// See: https://github.com/gradle/gradle/issues/19331
project.afterEvaluate {
    tasks.named("generateMetadataFileForPluginMavenPublication") {
        dependsOn("jar")
    }
}

// -----------------------------------------------------------------------------
// Shadow Plugin (from "com.brightsparklabs.gradle.baseline")
// -----------------------------------------------------------------------------

shadowJar {
    archiveClassifier = null
}
