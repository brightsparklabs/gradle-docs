/*
 * Maintained by brightSPARK Labs.
 * www.brightsparklabs.com
 *
 * Refer to LICENSE at repository root for license details.
 */

// -----------------------------------------------------------------------------
// PLUGINS
// -----------------------------------------------------------------------------

plugins {
    // Apply the Java Gradle plugin development plugin to add support for developing Gradle plugins
    id "java-gradle-plugin"
    id "com.gradle.plugin-publish" version "0.12.0"

    // Apply the Groovy plugin to add support for Groovy
    id "groovy"

    // Apply the Java plugin to add support for Java
    id "java"

    // Apply the Spotless plugin for formatting
    id "com.diffplug.spotless" version "5.8.2"
}

// -----------------------------------------------------------------------------
// REPOSITORIES/DEPENDENCIES
// -----------------------------------------------------------------------------

repositories {
    // Use jcenter for resolving dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

dependencies {
    implementation(
            'com.hubspot.jinjava:jinjava:2.5.4',
            'org.yaml:snakeyaml:1.26',
            // Add external Gradle plugins here so they can be added via our plugin
            // NOTE: Obtain plugin dependency strings from plugins.gradle.org page
            'org.asciidoctor:asciidoctor-gradle-jvm:3.3.0',
            'org.asciidoctor:asciidoctor-gradle-jvm-pdf:3.3.0',
            )
    // Use the awesome Spock testing and specification framework
    testImplementation "org.spockframework:spock-core:1.3-groovy-2.5"
}

// -----------------------------------------------------------------------------
// PROJECT IDENTIFICATION
// -----------------------------------------------------------------------------

project.description = "Applies brightSPARK Labs standardisation to project documentation"
project.group       = "com.brightsparklabs"

def versionProcess  = "git describe --always --dirty".execute()
versionProcess.waitFor()
project.version     = versionProcess.exitValue() == 0 ? versionProcess.text.trim() : "0.0.0-UNKNOWN"

project.ext.scm     = "https://github.com/brightsparklabs/gradle-docs.git"
project.ext.url     = "https://github.com/brightsparklabs/gradle-docs"

// -----------------------------------------------------------------------------
// java-gradle-plugin
// -----------------------------------------------------------------------------

gradlePlugin {
    // Define the plugin
    plugins {
        docs {
            id = "com.brightsparklabs.gradle.docs"
            implementationClass = "com.brightsparklabs.gradle.docs.DocsPlugin"
        }
    }
}

// Add a source set for the functional test suite
sourceSets {
    functionalTest {
    }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)
configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

// Add a task to run the functional tests
task functionalTest(type: Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

check {
    // Run the functional tests as part of `check`
    dependsOn(tasks.functionalTest)
}

pluginBundle {
    website = project.url
    vcsUrl = project.scm
    description = project.description
    tags = [
        "brightsparklabs",
        "gradle",
        "docs",
        "asciidoc",
        "asciidoctor",
    ]

    plugins {
        docs {
            displayName = "Applies brightSPARK Labs standardisation to project documentation"
        }
    }
}

// -----------------------------------------------------------------------------
// spotless
// -----------------------------------------------------------------------------

def header = """/*
               | * Maintained by brightSPARK Labs.
               | * www.brightsparklabs.com
               | *
               | * Refer to LICENSE at repository root for license details.
               | */
             """.stripMargin("|")
spotless {
    java {
        licenseHeader(header)
        googleJavaFormat().aosp()
    }
    groovy {
        licenseHeader(header)
        excludeJava() // excludes all Java sources within the Groovy source dirs from formatting
        // the Groovy Eclipse formatter extends the Java Eclipse formatter,
        // so it formats Java files by default (unless `excludeJava` is used).
        greclipse()
        indentWithSpaces(4)
    }
    groovyGradle {
        // same as groovy, but for .gradle (defaults to "*.gradle")

        // unfortunately licenseHeader is currently unsupported on groovyGradle
        //licenseHeader(header)

        greclipse()
        indentWithSpaces(4)
    }
}
